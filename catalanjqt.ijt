NB. Lab: Video - Generating the Catalan numbers
NB. 
NB.labchapter: 0 - Video Requirements

0 : 0
_______________________________________

To run this video lab, you must have 
access to youtube.com through the 
internet. 

If you do not, the lab will still 
advance through the exercises, but the 
videos will not be displayed.

Videos will appear as steps in the lab,
but you will need to click play on the video 
to get it started.

When you have viewed a video, select 
the line below the video and continue 
on as you would for any other lab.

This is required because when you are 
interacting with a video, you are not 
actually interacting with the J lab.

Also, this lab will define verbs that you
can use to explore the concepts. It will 
place these verbs in the Cat locale and 
add this to the path of your current locale.
In the case of name conflicts the verbs
may be accessed directly by adding _Cat_ to
the verb name. eg. catalan_Cat_ 
_______________________________________
)

NB.labchapter: 1 - Catalan numbers
NB.labprepare:set up Jqt display and define test and test 3
require jpath '~addons/media/videolabs/videolabjqt.ijs'
init_qtvideo_ ''  NB. initialize according to screensize
display_qtvideo_ '5p8--DMRO2U'

coinsert 'Cat' NB. Put Cat locale into copath of current locale

tester_Cat_=: 4 : 0  NB. Basis of catalan tester for string y of length 2 x
if. 2~:3!:0 y do.(LF,(":y),' is not a character string.',LF,'Try entering: test',(":x),' ''',(":y),'''',LF)return. end.
y=.y-.' ' NB. remove blanks in string 
if. -.*./ y e.'10' do.(LF,'''',y,''' should only contain ''0''s and ''1''s. No spaces.',LF)return. end. 
if. (+:x)>#y do. (LF,'''',y,''' needs ',(": t),' more character',((1<t=.(+:x)-#y){' s'),LF) return. end.
if. (+:x)<#y do. (LF,'''',y,''' needs ',(": t),' less character',((1<t=.(#y)-+:x){' s'),LF) return. end.
if. | t=.* +/r=.(+ _1* -. ) ". 1j1 # y do.(LF,'''',y,''' needs more ''',(":(0>t){ 0 1),'''s in string',LF) return. end. 
if. 0><./t=.+/\ r do.(LF,'''',y,''' breaks rule by having more ''0''s than ''1''s at position: ',(": t i. _1),LF)return. end. 
LF,'''',y, ''' is valid',LF
)

test3_Cat_=: 3 & tester  NB. test string of length 6 for Catalan validity

NB.labprepare:

0 : 0
_______________________________________

Now, see if you can find the five Catalan
strings with 3 one's and 3 zero's using test3.

test3 '101010'
_______________________________________
)

NB.labchapter: 2 - Other ways of finding Catalan strings
NB.labprepare: display and define test4
display_qtvideo_ 'm5K1zWbyQCQ'

test4_Cat_=: 4 & tester  NB. test string of length 8 for Catalan validity
NB.labprepare:

0 : 0
_______________________________________

Now, can you find the 14 Catalan strings
with 4 one's and 4 zero's using test4?

test4 '10101010'
_______________________________________
)

NB.labchapter: 3 - What can Catalan strings represent?

NB.labprepare: display and define test4
display_qtvideo_ 'IJkyzPHRiGY'

CSS_Cat_=: 0 : 0  NB. CSS for the different Catalan views.
<style type="text/css">
 svg {overflow:visible;}
 text { font-family:"courier"; font-weight:bold; stroke-width:0.75; stroke:black; pointer-events: none; text-anchor:start; white-space:pre;}
 .pr  { fill:#888; stroke-width:0.4; font-weight:bold; font-size: 30px;}
 .pl , .b1  { fill:#000; stroke-width:0.4; font-weight:bold; font-size: 30px;} 
 .br , .b0 { fill:#F00; stroke-width:0.4; font-weight:bold; font-size: 30px;} 
 .bb  { fill:#00F; stroke-width:0.4; font-weight:bold; font-size: 30px;} 
 .mr  { fill:#F00; stroke-width:0.4; font-weight:bold; font-size: 30px;}
 .mb  { stroke:blue; stroke-width:8; font-weight:bold; font-size: 30px;}  
 .wr  { fill:#FFF; stroke-width:0; font-weight:bold; font-size: 30px;}
 .polr  { stroke:#900; stroke-width:1.5;}
 .pg  { stroke:#AAA; stroke-width:13; fill:none;}
 .m   { stroke:#00F; stroke-width:4; fill:none;}  
</style>
)

htmpack_Cat_=: 3 :'''<hmtl><head><meta charset="UTF-8">'', (CSS rplc ''<FONT>'';''courier''),''</head><body>'', y ,''</body></html>'''

DISPLAY_Cat_=: 0 : 0 NB. Form that contains the display of the different catalan views
pc enhanced;
cc w1 webview;
)

webdisplay_Cat_=: 4 : 0 NB. Displays the results of different Catalan views in webview 
wd DISPLAY
wd 'pn *', > {. x
wd 'pmove ',(": -: 2 { ". wd 'qscreen'),' 100 _1 _1'
wd 'set w1 wh *', ": 200 120>. (_225 _200 + 2 3 { ". wd 'qscreen') <. 1.1 * > {: x
wd 'set w1 html *', ,y
wd 'pshow'
)

enhanced_close_Cat_=: 3 : 0
handle=. ": wd 'qhwndp;' 
wd 'psel ',handle,';'
wd 'pclose;'
)

jhtml_Cat_=: 4 : 0
'w h'=: > {: x
x webdisplay htmpack '<svg width="',(": w),'" height="',(": h),'" >',y,'</svg>'
)

cmb_Cat_=:#:@:i.@:(2&^)@:+:@:<: NB. creates the truth table for all combinations
r1_Cat_=:((-:@:# = +/)"_1 # ]) NB. removes those combinations where number of 1's ~: number of 0's - Rule 1
r2_Cat_=: ((_1&<:@: (<./)@:(+/\)@:(+ (_1 * -.))"_1) # ]) NB. removes those combinations where number of 0's > number of preceding 1's- Rule 2
cat_Cat_=: a:&$: : (<@: ({ ($~)`(1 0"_)`((1 , 0 ,~ ])"1 @: r2 @: r1 @: cmb)@.(2&<.))"0) NB. Handles 0 and 1 cases and trims others with the beginning 1 column and ending 0 column

NB. Verbs for lesson 4
parenthesis_Cat_=: 3 : 0
(i. catalan y) parenthesis y
:
if. y=0 do. (('Parenthesis - ' , ": y); 100 100) jhtml '<rect x="1" y="1" width="10" height="10" stroke="grey" fill="grey" /><text x="15" y="30" class="pl" > Empty </text>' return. end. 
if. y=1 do. (('Parenthesis - ' , ": y); 100 100) jhtml '<text x="20" y="40"><tspan class="pl">(</tspan><tspan class="pr">)</tspan></text>' return. end.
if. 8<y do. (('Parenthesis - ' , ": y); 680 100) jhtml '<text x="15" y="50" class="pl" >8 is the maximum value for this display.</text>' return. end.  NB. artificial limit for display
tm=.'<text  x="10" y="20" style=";font-size:20px"> ', ,&'</text>' ;(<'<tspan x="10" dy="35"> '),"1 ,.&(<'</tspan>') x (('<tspan class="pr">)</tspan>';'<tspan class="pl">(</tspan>') {~ >@:cat)y
(('Parenthesis - ' , ": y); (35 * # > 1 cat y) , (35 * >: #x)) jhtml tm
)
ballot_Cat_=:  3 : 0
(i. catalan y) ballot y
: 
if. y=0 do. (('Ballot - ' , ": y); 100 100) jhtml '<rect x="1" y="1" width="10" height="10" stroke="grey" fill="grey" /><text x="15" y="30" class="pl" > Empty </text>' return. end. 
if. y=1 do. (('Ballot - ' , ": y); 100 100) jhtml '<text x="20" y="40"><tspan class="br">R</tspan><tspan class="bb">B</tspan></text>' return. end.
if. 8<y do. (('Ballot - ' , ": y); 680 100) jhtml '<text x="15" y="50" class="pl" >8 is the maximum value for this display.</text>' return. end.  NB. artificial limit for display
tm=.'<text  x="10" y="20" style=";font-size:20px"> ', ,&'</text>' ;(<'<tspan x="10" dy="35"> '),"1 ,.&(<'</tspan>') x (('<tspan class="br">R</tspan>';'<tspan class="bb">B</tspan>') {~ >@:cat)y
(('Ballot - ' , ": y); (35 * # > 1 cat y) , (35 * >: #x)) jhtml tm
)

binary_Cat_=: 3 : 0
(i. catalan y) binary y 
:
if. y=0 do. ' Empty ' return. end. 
if. y=1 do. '1 0' return. end.
if. 8<y do. (('Binary - ' , ": y); 680 100) jhtml '<text x="15" y="50" class="pl" >8 is the maximum value for this display.</text>' return. end.  NB. artificial limit for display
 x >@:cat y 
)

ct_Cat_=:(+ (_1 * -.)) @>@cat
exf_Cat_=:i.@(#"1)@ct
wyf_Cat_=: ( (+/\"1 )@ct - >@cat)
frmt_Cat_=:,@:(];;@:(('<tspan class="','">',~[);'</tspan>';~]))

mount_Cat_=: 3 : 0
(i. catalan y) mount y
:
if. y=0 do. <' . Empty' return. end.
if. y=1 do. <'/\' return. end.
t=.(' ' $~ catalan , ] , +:) y NB.(0,x) (' ' <@$~ (([: >./"1 >:@wyf) ,"0 +:@:] ))y
t=. (x (<. <:@:catalan) y){ <"_1 ('\/'{~ > a: cat y)((a:([:<"1(i.@:catalan@],."0 _1 wyf_Cat_,"0 exf_Cat_))y))}"_ t
_6 ]\ |. each t
)

multiply_Cat_=: 3 : 0
(i. catalan y) multiply y
:
if. y=0 do. '() Empty' return. end. 
if. y=1 do. '(a*b)' return. end.
if. 9<y do. '9 is the maximum value for this display.' return. end. NB. artificial limit of variables for display
t=.({"_1 (')',.'abcdefghij'&({~ <:@:(+/\))))"1@:(1,.>@:cat) y
mult"1 <"0 x{t
)

mult_Cat_=: 3 : 0
p=.y
while. 1<# p do.
 for_t. p do. if. t=<')' do. p=. t_index (((-&2)@:[ {. ]) , <@:(')',~'(',;@:(_2&+@[}.{.)) , (>:@:[ }. ])) p break.end. end.
end.
p=.(;p) rplc '(a';'(a*';'(b';'(b*';'(c';'(c*';'(d';'(d*';'(e';'(e*';'(f';'(f*';'(g';'(g*';'(h';'(h*';'(i';'(i*';'(j';'(j*';')c';')*c';')d';')*d';')e';')*e';')f';')*f';')g';')*g';')h';')*h';')i';')*i';')j';')*j'
p rplc ')(';')*('
)

path_Cat_=: 3 : 0
(i. catalan y) path y
:
if. y=0 do. (('Path - ' , ": y); 100 100) jhtml '<rect x="2" y="2" width="10" height="10" class="pg" /><text x="15" y="30" class="pl" > Empty </text>' return. end. 
if. y=1 do. t=. '<rect x="2" y="2" width="10" height="10" class="pg" />'
            t=. t,'<line x1="2" y1="12" x2="12" y2="2" stroke="red" />'
            (('Path - ' , ": y); 100 100) jhtml t,'<path stroke="forestgreen" stroke-width="3" fill="none" d="M 1,13 v -11 h 11" />' return. end.
if. 7<y do. (('Path - ' , ": y); 680 100) jhtml '<text x="15" y="50" class="pl" >7 is the maximum value for this display.</text>' return. end. NB. artificial limit of variables for display
pos=.x{ >@:({. ,@:{@:(1&*@:i.@:>.@:%&8 ; 1.2&*@:i.@:<.&8))@:catalan  y NB. upper left positions of grids
(('Path - ' , ": y); ((5 + (8<.catalan) *  11 * ]) ,( >.@:(8 %~ catalan)* 10 * >:)) y) jhtml  , pos paths > (x (<. <:@:catalan) y) cat y
)
paths_Cat_=: 4 : 0"1 NB. aux to build grids N1 AUX N2 - N1 is x1 y1 - N2 is size of grid in rows=columns
'h w'=.x
n=. -: # y
t=.('<line x1="',":@:(xpos + (wid =. _10 + (10 * >:) ) ),'" y1="',(": ypos),'" x2="',":@:(xpos=. 10 * *&w),'" y2="',":@:((ht=. _10 + (10 * >:))  + (ypos=. 10 + 10 * (h * >:) )),'" stroke="red" stroke-width="1.5" />'"_) n
t=. t,('<rect x="',(":xpos),'" y="',(":ypos),'" width="',(":wid),'" height="',(":ht),'" stroke="grey" fill="none" />'"_) n
t=. t,,('<line x1="',"1 (": xpos) ,"1 '" y1="',"1(": ypos + 10 * ,.@:i.),"1 '" x2="' ,"1 (": wid+xpos),"1 '" y2="',"1 (": ypos + 10 * ,.@:i.),"1'" stroke="gray"/>'"_) n
t=. t,,('<line x1="',"1 (": xpos + 10 * ,.@:i.) ,"1 '" y1="',"1(": ypos ),"1 '" x2="' ,"1 (": xpos + 10 * ,.@:i.),"1 '" y2="',"1 (": ypos + ht ),"1'" stroke="gray"/>'"_) n
t,,('<path stroke="forestgreen" stroke-width="4" fill="none" d="M ',":@:(xpos@:-:@:#),',', ":@:((ht + ypos) & -:@:#),((' h 10';' v -10');@:{~ ]),'" />'"_) y NB. forest green #228B22
)

polygon_Cat_=: 3 : 0
(i. catalan 8 <.y) polygon y
:
t=. 8 <. y
if. y=0 do. (('Polygon - ' , ": y); 100 100) jhtml '<rect x="1" y="1" width="10" height="10" stroke="grey" fill="grey" /><text x="15" y="30" class="pl" > Empty </text>' return. end. 
if. y=1 do. t=. '<line class="polr" x1="39.142" y1="39.142" x2="5.681" y2="30.176" />'
            t=. t,'<line class="polr" x1="30.176" y1="5.681" x2="5.681" y2="30.176" />'
            (('Polygon - ' , ": y); 100 100) jhtml t,'<line class="polr" x1="39.142" y1="39.142" x2="30.176" y2="5.681"x />' return. end.
if. 7<y do. (('Path - ' , ": y); 680 100) jhtml '<text x="15" y="50" class="pl" >7 is the maximum value for this display.</text>' return. end. NB. artificial limit of variables for display
pos=.(i. # x (<. <:@:catalan) t){ >@:({. ,@:{@:(1.2&*@:i.@:>.@:%&10 ; 1.2&*@:i.@:<.&10))@:catalan  t NB. upper left positions of grids
vertices=.(25 +|. pos* 40) (+"1"1 _) 20+.@:r.((o.%4)+ 2&% * o.@:i.)t+2
ed=. ,/ poly (1,. > cat t) # inv"_1  (;"1) 2[\"1 <"1 vertices
w=.('<svg height="',":@: ( 48 * >.@:((#x)<.10 %~ catalan)),'" width="',":@:(125 * <.&10),'">'"_)t NB. outside svg wrapper 8 grids per row
(('Polygon - ' , ": y); (47 * (1.2+ {: >./pos) <. 10),( 48 * >. ((#x)<.10 %~ catalan y))) jhtml   ; ": each (<'<line x1="'),.(<"0 [ 1{"1 ed),. (<'" y1="'),.(<"0 {."1 ed),.(<'" x2="'),. (<"0 {:"1 ed),.(<'" y2="'),. ( _2{"1 ed);"0(<'" class="polr" />')
)
poly_Cat_=: 3 : 0"_1
t=.y
p=.0
while. p<#t do.
if. */ p{t 
 do. p=.p+1 
 else. t=. ((_2{.p&{. ),(_2 }. p&{. ), ((2{. (p-2)&{),(_2{. (p-1)&{ )) , (p+1)&}.) t end.
 end.
 t
)
pexp_Cat_=: [: +/ ] <.@% [ ^ >: @ i. @ (<.@^.)
ple_Cat_=: p: @ i. @ (p: inverse) @ >:

catalan_Cat_=: 3 : 0
p=. ple +: y NB. the list of primes less than 2 * n
 e1=. p pexp"0 +: y NB. The exponents of primes of the numerator !2 * n
 e2=. p pexp"0 y NB. The exponents of primes of the denominator ! n
 e3=. p pexp"0 >: y NB. The exponents of primes of the denominator ! >: n
*/  p ^ x: e1-e2+e3 NB. Add and subtract exponents then restore to their true values and multiply for the answer.
)

NB.labprepare:

0 : 0
_______________________________________

Look at these different views of Catalan strings using:

binary n
parenthesis n
ballot n
multiply n
path n
polygon n
mount n

where n can be any integer up to about 7
although some can go higher.
_______________________________________
)

NB.labchapter: 4 - How long would it take if we counted strings?

NB.labprepare: display and define firstcat
display_qtvideo_ 'jtfl7Z7gX5M'
firstcat_Cat_=: (1:`1:`#@.(2<.#@$))@>@cat
NB.labprepare:

0 : 0
_______________________________________

firstcat is not recommended as a way to
create Catalan numbers, but try out a couple 
values anyway.

firstcat 6

If you want to get a sense of the time and
space then run
timespacex 'firstcat n'
where n is any number up to about 10
depending on how long you want to wait.
_______________________________________
)

NB.labchapter: 5 - Explaining the numerator of the Catalan formula

NB.labprepare: display and define explode
display_qtvideo_ 'CiOeRVN_bZk'
explode_Cat_=:  (! +:)@:x:
NB.labprepare:

0 : 0
_______________________________________

explode is a verb that works pretty 
quick but the results grow pretty quick 
as well. See if you can make things go
pop.


explode 45
_______________________________________
)


NB.labchapter: 6 - Explaining the denominator of the Catalan formula

NB.labprepare: display and define kitty
display_qtvideo_ 'WlTImA01gHM'
kitty_Cat_=: (! +:) % >:
NB.labprepare:

0 : 0
_______________________________________

kitty is a verb that will calculate
catalan numbers up to a point. It won't
get you all the way to a million, but it
can do numbers you wouldn't want to do
by hand.

kitty 11
_______________________________________
)

NB.labchapter: 7 - Extending the accuracy of our Catalan formula

NB.labprepare: display and define kitty
display_qtvideo_ 'vmEAy9beo6U'
kittyX_Cat_=:  kitty @ x:
NB.labprepare:

0 : 0
_______________________________________

In order to get the precision that we
require we use the Extended monadic verb (x:)
to change kitty into kittyX
__________________________
)

(kittyX ; kitty) 45

NB.labchapter: 8 - Logarithms - one of our secret weapons

NB.labprepare: display
display_qtvideo_ 'yWKG-whuQMU'
NB.labprepare:

0 : 0
_______________________________________

Power (^) and Logarithm (^.) are built in to J
as primitive dyadic verbs which means that
you access them directly from their symbols.

______________________________________
)

NB.labchapter: 9 - Finding the Prime Factors of a number

NB.labprepare: display
display_qtvideo_ 'oS6SL65NZFs'
NB.labprepare:

0 : 0
_______________________________________

Prime Factors (q:) is a primitve monadic
verb that returns the prime factors of a 
given integer.

q: 45
_______________________________________
)

NB.labchapter: 10 - Using Prime Factors to simplify calculations

NB.labprepare: display
display_qtvideo_ 'W9gZlLunGA8'
NB.labprepare:

0 : 0
_______________________________________

You can play with Power (^) and Logarithm (^.)
to show how multiplication is the same as 
adding logarithms and division is the same as 
subtracting logarithms.

(81 * 20) -: 3 ^ (3 ^. 81) + (3 ^. 20)
_______________________________________
)

NB.labchapter: 11 - Finding the primes less than a given integer

NB.labprepare: display and define ple
display_qtvideo_ 'MNDnVp-AplA'
ple_Cat_=: i. &. (p: inverse)
NB.labprepare:

0 : 0
_______________________________________

ple is a defined verb that returns a list
of primes less than the given integer.

ple =: i. &. (p: inverse)

ple 45
_______________________________________
)

NB.labchapter: 12 - Finding the exponents of the primes in the factorial of a given integer

NB.labprepare: display and define pexp
display_qtvideo_ 'bBlINpzzUCA'
pexp_Cat_=:  [: +/ ] <.@% [ ^ >: @ i. @ (<.@^.)
NB.labprepare:

0 : 0
_______________________________________

pexp is a defined verb that returns the
number of times a prime occurs in the 
factorial of a given integer.

pexp =:  [: +/ ] <.@% [ ^ >: @ i. @ (<.@^.)

3 pexp 45
_______________________________________
)


NB.labchapter: 13 - Finding the millionth Catalan number

NB.labprepare: display and define catalan and kittyN
display_qtvideo_ 'ez2OkKU6MxY'
catalan=: 3 : 0
p=. ple +: y NB. the list of primes less than 2 * n
 e1=. p pexp"0 +: y NB. The exponents of primes of the numerator !2 * n
 e2=. p pexp"0 y NB. The exponents of primes of the denominator ! n
 e3=. p pexp"0 >: y NB. The exponents of primes of the denominator ! >: n
*/  p ^ x: e1-e2+e3 NB. Add and subtract exponents then restore to their true values and multiply for the answer.
)
kittyN_Cat_=:  #@:":@: catalan_Cat_
NB.labprepare:

0 : 0
_______________________________________

catalan will return the catalan number for
a given integer up to very large numbers, but
it may be more effective to display the number
of integers in the number to avoid too many
pages required to display the Catalan number
by using kittyN. 

kittyN 50000
_______________________________________
)

0 : 0
_______________________________________

You can use kittyN to try to find the 
number of digits in the millionth catalan 
number if you would like. This may 
take a few minutes to calculate.

kittyN 1000000

_______________________________________
)
NB.labchapter: 14 - References

0 : 0
_______________________________________

Professor Koo-Guan Choo 
Senior Lecturer in the School of Mathematics and Statistics at the University of Sydney
http://www.maths.usyd.edu.au/u/kooc/catalan.html

Josef Rukavicka
On Generalized Dyck Paths 
http://www.emis.de/journals/EJC/ Volume_18/PDF/v18i1p40.pdf
 _______________________________________
)

0 : 0
_______________________________________

If you would like to review any of the
previous chapters, advance the lab then 
position the cursor on the chapter you
wish to review and press return.
_______________________________________
)

lab ':'
